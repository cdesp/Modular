NAME PS/2 KEYBOARD

;DEVPS2 EQU $46 ; SET ON MAIN
;COPSTA EQU 6	; SET ON MAIN
GLOBAL PS2KEY
GLOBAL PS2ISK
GLOBAL PS2LOK
GLOBAL NBLOOK
GLOBAL GTKEY
GLOBAL GTNBKEY

;WAIT FOR A KEY TO BE PRESSED
PS2KEY:		IN A,(COPSTA)	
					BIT 5,A
					JR Z, PS2KEY 			; TEST KEYB INTERRUPT
					IN A,(DEVPS2) 		;TAKE THE KEY
					OUT (DEVPS2+1),A  ;SIGNAL WE GOT THE KEY
					RET

;IS ANY KEY PRESSED
PS2ISK:		IN A,(COPSTA)	
					BIT 5,A
					JR Z, PS2NTP
					LD A,1				;SET A TO 1 MEANS WE HAVE A KEY
					RET
PS2NTP		XOR A					;SET A TO 0 MEANS NO KEY AVAILABLE
					RET				

GTNBKEY	CALL GTKEY
				CP 0
				RET Z
		    CALL NBLOOK
		    RET
		    
GTKEY:	CALL PS2KEY	;GET A KEY FROM PS2
	CP $E1		;BREAK KEY
	CALL Z,GTBRK
	CP $F0		;240 KEY RELEASED
	CALL Z,GTREL
	CP $E0		;224 EXTENDED KEYS
	CALL Z,GTEXT
 

	CP 18		;LEFT SHIFT
	CALL Z,SHFTOG
	CP 89		;RIGHT SHIFT
	CALL Z,SHFTOG
	CP 20		;RIGHT CONTROL
	CALL Z,CTRTOG	
	CP 17		;LEFT ALT
	CALL Z,ALTTOG
	RET

GTBRK:	CALL PS2KEY	;$14 OR $F0
	CP $F0
	RET Z
	CALL PS2KEY	;$77
	RET

GTEXT:	CALL PS2KEY	; THIS IS THE EXTENDED KEY
	CP $6B  ;LEFT
	RET Z
	CP $72	;DOWN
	RET Z
	CP $74	;RIGHT
	RET Z
	CP $75	;UP
	RET Z
	CP $70	;INSERT
	RET Z
	CP $6C	;HOME
	RET Z
	CP $7D	;PG UP
	RET Z
	CP $7A	;PG DN
	RET Z
	CP $69	;END
	RET Z
	CP $71	;DEL
	RET Z
	CP $5A	;ENTER NUM
	RET Z
	CP $11	;RIGHT ALT
	RET Z
	CP $14	;LEFT ALT
	RET Z
	CP $F0
	JP NZ, 	EXGTEX	;GTREL ;EXTENDED KEY RELEASED $E0 $F0 $KEY
	CALL PS2KEY	; THIS IS THE EXTENDED RELEASED KEY JUST IGNORE IT

EXGTEX	XOR A	;NOT VALID EXTENDED KEY
	RET


GTREL:	CALL PS2KEY	; THIS IS THE RELEASED KEY
	CP 18		; LSHIFT RELEASED
	CALL Z,SHFRES	; RESET SHIFT
	CP 20		; LCTRL RELEASED
	CALL Z,CTRRES	; RESET CONTROL
	CP 17		; LALT RELEASED
	CALL Z,ALTRES	; RESET ALT
	CP 89		; RSHIFT RELEASED
	CALL Z,SHFRES	; RESET SHIFT
	XOR A		; NO KEY
	RET

SHFRES:	LD HL,KEYCTR	;KEYBOARD FLAGS SHFT,CONTROL,GRAPH 
	RES 6,(HL)  	; BIT 6 IS SHIFT $40=64
	RET

CTRRES:	LD HL,KEYCTR	;KEYBOARD FLAGS SHFT,CONTROL,GRAPH
	RES 7,(HL)	; BIT 7 IS CTRL $80=128
	RET

ALTRES:	LD HL,KEYCTR	;KEYBOARD FLAGS SHFT,CONTROL,GRAPH
	RES 5,(HL)	; BIT 5 IS SHIFT $20=32 
	RET

SHFTOG:	LD HL,KEYCTR	;KEYBOARD FLAGS SHFT,CONTROL,GRAPH 
	LD A,$40	; MASK
	XOR (HL)  	; BIT 6 IS SHIFT $40=64
	LD (HL),A	; SAVE
	XOR A		; NO KEY
	RET

CTRTOG:	LD HL,KEYCTR	;KEYBOARD FLAGS SHFT,CONTROL,GRAPH
	LD A,$80	; MASK
	XOR (HL)	; BIT 7 IS CTRL $80=128
	LD (HL),A	; SAVE
	XOR A		; NO KEY
	RET

ALTTOG:	LD HL,KEYCTR	;KEYBOARD FLAGS SHFT,CONTROL,GRAPH
	LD A,$20
	XOR (HL)	; BIT 5 IS ALT $20=32 
	LD (HL),A	; SAVE
	XOR A		; NO KEY
	RET



					
PS2LOK	LD HL,PS2TAB
	LD B,0
	LD C,A
	ADD HL,BC
	LD A,(HL)
	LD HL,KEYCTR
	LD B,(HL)
	BIT 6,B			;BIT 6 IS SHIFT
	RET Z
	ADD A,32
	RET

NBLOOK	LD HL,NBTAB
	LD B,0
	LD C,A
	ADD HL,BC
	LD A,(HL)
	CP 0
	RET Z
	LD HL,KEYCTR	;BIT 6=SHIFT BIT 7=CTRL  BIT 6+7=GRPH	
	BIT 5,(HL)
	JR Z,NOGRPH
	LD B,192	;SET BIT 6 AND 7
	JR NBLKEX
NOGRPH	LD C,A
		LD A,(HL)
		AND 11000000
		LD B,A
		LD A,C			
NBLKEX	OR B
	RET





PS2TAB	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,'`',0,0,0,0,0
	DB 0,'Q','1',0,0,0,'Z','S','A','W'
	DB '2',0,0,'C','X','D','E','4','3',0
	DB 0,' ','V','F','T','R','5',0,0,'N'
	DB 'B','H','G','Y','6',0,0,0,'M','J'
	DB 'U','7','8',0,0,',','K','I','O','0'
	DB '9',0,0,'.','/','L',59,'P','-',0
	DB 0,0,39,0,'[','=',0,0,0,0
	DB 13,']',0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,'3',0,'*','9',0,0,0,0



NBTAB	DB 0,0,0,0,0,0,0,0,0,0		;  0-  9
	DB 0,0,0,36+128,0,0,0,0,0,0		; 10- 19	0,0,0,TAB
	DB 0,14,9,0,0,0,60,45,46,13	; 20- 29
	DB 8,0,0,58,59,44,12,6,7,0	; 30- 39
	DB 0,15,57,43,10,11,5,0,0,52	; 40- 49
	DB 56,41,42,37,4,0,0,0,53,51	; 50- 59
	DB 35,3,19,0,0,54,62,36,38,21	; 60- 69
	DB 20,0,0,55,47,39,40,26,28,0	; 70- 79
	DB 0,0,67,0,86,27,0,0,0,0	; 80- 89
	DB 30,0,0,0,0,0,0,0,0,0		; 90- 99
	DB 0,0,64,0,0,26+128,0,2,63,0		;100-109  0,0,64,0,0,END
	DB 0,0,61,37+128,34,0,18,50,31,25	;110-119  0,0,INS,DEL
	DB 0,29,7,28,24,20,0,0,0,0	;120-129											