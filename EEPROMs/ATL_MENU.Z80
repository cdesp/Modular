;HANDLE MENUS

		GLOBAL CREATEMENU

;COUNTS THE MENU ITEMS AND THE MAX LENGTH
;HL THE FIRST MENU ZERO TERMINATED ITEM 
;MENU ITEMS TERMINATED BY DOUBLE 0
;RETURNS C NUMBER OF ITEMS
;RETURNS B MAX ITEM LENGTH
MNU_CNTITMS: 	LD C,0			;ITEM COUNTER
		LD B,0			;ITEM LENGTH COUNTER
		LD D,0			;TEMP ITEM LENGTH COUNTER
MNU_CHKNXT:	LD A,(HL)
		OR A
		RET Z			;RETURN IF MENU ITEM STARTS WITH ZERO
		INC C			;WE HAVE A NEW ITEM
		LD D,0
MNU_LPITM:	INC HL
		INC D
		LD A,B
		CP D
		JR NC,MNU_NOTBIGER
		LD B,D			;SAVE THE BIGGER ITEMS LENGTH		
MNU_NOTBIGER:	LD A,(HL)
		OR A
		JR NZ, MNU_LPITM
		INC HL
		JR MNU_CHKNXT
	
PRINTMNUITM:	PUSH IX
		PUSH BC
		CALL OS_PRINTTEXT
		POP BC
		POP IX
		RET

;REVERSES THE VGA COLORS
VGA_REVMNUCLR:	LD A,(MENUVCLR)
		RLCA
		RLCA
		RLCA
		RLCA
		LD (MENUVCLR),A
		LD HL,(MENULFORE)
		LD (BCOLOR),HL
		LD HL,(MENULBACK)
		LD (FCOLOR),HL
		RET

;A=0 DEFAULT COLORS A=1 REVERSE COLORS
SETITMCOLOR:	CP 0
		JR Z,DEFCOLOR
		CALL VGA_REVMNUCLR
		JR SETVGACOL					
DEFCOLOR:	LD HL,(MENULFORE)
		LD (FCOLOR),HL
		LD HL,(MENULBACK)
		LD (BCOLOR),HL
SETVGACOL:	LD A,(MENUVCLR)
		PUSH AF
		AND $0F
		LD (VGAFCOL),A
		POP AF
		RLCA
		RLCA
		RLCA
		RLCA
		AND $0F
		LD (VGABCOL),A
		RET


;A=0 DEFAULT COLORS A=1 REVERSE COLORS
RESETITMCOLOR:	CP 0
		RET Z
;REVERSE THE COLORS
		CALL VGA_REVMNUCLR
		LD HL,(MENULFORE)
		LD (FCOLOR),HL
		LD HL,(MENULBACK)
		LD (BCOLOR),HL
		RET


;HL THE MENU TO PRINT
;DE THE POSITION X,Y OF THE MENU
;A THE SELECTED MENU ITEM
PRINTMENU:	PUSH AF
		PUSH HL
		PUSH DE		
		CALL OS_GOTOXY		;SET CURSOR TO X,Y=D,E
		CALL MNU_CNTITMS	;C THE ITEM NUMBER B THE MAX LENGTH
		PUSH BC
		LD IX,0
		ADD IX,SP		
		PUSH BC
		CALL VGA_GETCOL		;GET VGA COLOR ON A
		POP BC		
		PUSH AF			;SAVE COLOR INFO
		LD HL,(FCOLOR)
		PUSH HL			;SAVE COLOR INFO
		LD HL,(BCOLOR)
		PUSH HL			;SAVE COLOR INFO
		LD A,0			;SET MENU COLORS
		CALL SETITMCOLOR
		LD HL,STRGBUFF		;USE STRGBUFF TO CREATE THE OUT TEXT
		PUSH HL
		;PRINT 1ST LINE
		INC B
		LD (HL),139		;'/'
PM_AGN1:	INC HL
		LD (HL),129		;'='
		DJNZ PM_AGN1
		INC HL
		LD (HL),138		;'\'
		INC HL
		LD (HL),0
		POP HL
		CALL PRINTMNUITM
		LD B,1			;;1ST ITEM
		LD H,(IX+5)		;MENU ITEM
		LD L,(IX+4)
PM_AGN3:	LD DE,STRGBUFF
		LD A,130		;'|'
		LD (DE),A
		LD A,(IX+07)		;GET SELECTED ITEM
		CP B
		LD A,' '
		JR NZ, PM_SKP2
		LD A,197		;'>'
PM_SKP2:	INC DE
		LD (DE),A		
		LD C,0			;COUNT CHARS PRINTED
PM_AGN2:	LD A,(HL)
		INC DE
		OR A
		JR Z,PM_NXT1		;ITEM END				
		LD (DE),A
		INC HL
		INC C
		JR PM_AGN2
PM_NXT1:	PUSH HL			;SAVE MENU ITEMS IDX
		PUSH BC
		LD A,(IX+1)		;A MAX LENGTH
		SUB C
		LD B,A
		CP 0
		JR Z,NOSPC		
		LD A,' '		
		LD (DE),A
		INC DE
		DJNZ $-2		
NOSPC:		POP BC		 
		LD A,130		;'|'
		LD (DE),A		
		INC DE		
		XOR A
		LD (DE),A		
		LD D,(IX+3)		;X,Y
		LD A,(IX+2)
		ADD A,B			;ADD MENU ITEM NO
		INC B
		LD E,A
		CALL OS_GOTOXY		;GOTO XY
		PUSH BC
		LD A,(IX+07)		;GET SELECTED ITEM
		INC A			;1 BASED
		CP B
		LD A,0			;NO CHANGE OF COLORS
		JR NZ,NOSELEC
		;CHANGE COLORS AND PAINT
		INC A
NOSELEC:	PUSH AF
		CALL SETITMCOLOR	;SETS ITEM COLOR
		LD HL,STRGBUFF
		CALL PRINTMNUITM
		POP AF
		CALL RESETITMCOLOR	;RESETS THE COLOR IF REVERSED
		POP BC
		POP HL			;GET NEXT MENU ITEM
		INC HL
		LD A,(HL)
		OR A
		JR NZ,PM_AGN3
		LD B,(IX+1)		;GET STRING LENGTH AT B
		LD C,(IX+0)
		;PRINT LAST LINE
		CALL SETITMCOLOR	;SETS ITEM COLOR
		LD HL,STRGBUFF		;USE STRGBUFF TO CREATE THE OUT TEXT
		PUSH HL
		LD (HL),137		;'\'
		INC B
PM_AGN4:	INC HL
		LD (HL),129		;'='
		DJNZ PM_AGN4
		INC HL
		LD (HL),136		;'/'
		INC HL
		LD (HL),0		
		LD D,(IX+3)		;X,Y
		LD A,(IX+2)
		ADD A,C
		INC A
		LD E,A
		CALL OS_GOTOXY
		POP HL
		CALL PRINTMNUITM
		;GET OUT RESTORE COLORS
		POP HL
		LD (BCOLOR),HL
		POP HL
		LD (FCOLOR),HL
		POP AF
		CALL VGA_SETCOL		;RESTORE ORIGINAL COLORS
		POP BC
		POP DE
		POP HL
		POP AF
		RET		
		

;HL THE MENU TO PRINT
;DE THE POSITION X,Y OF THE MENU
;A THE SELECTED MENU ITEM
;RETURN A THE SELECTED ITEM 1..MAXITEM
CREATEMENU:	PUSH HL			;SAVE MENU POSITION AND INFO
		PUSH DE
		PUSH AF
		CALL PRINTMENU
CM_GTKEY:	CALL GET_CHAR
		POP BC			;CURRENT ITEM SELECTED ON A
		CP 11			;ARROW UP
		JR NZ,CM_CHKNXT
		LD A,B
		CP 1
		PUSH BC
		JR Z,CM_GTKEY		;NO CHANGE
		POP AF
		DEC A
		PUSH AF
		JR REPRINT		
CM_CHKNXT:	CP 10			;ARROW DOWN
		JR NZ,CM_CHKNXT1
		POP DE
		POP HL
		PUSH HL
		PUSH DE
		PUSH BC		
		CALL MNU_CNTITMS	;C THE MENU ITEMS
		POP AF			;A THE CURRENT ITEM NO
		CP C			;WE ARE AT THE LAST?
		PUSH AF
		JR Z,CM_GTKEY		;NO CHANGE
		POP AF
		INC A
		PUSH AF
		;JR REPRINT		
REPRINT:	POP AF
		POP DE
		POP HL
		JR CREATEMENU
CM_CHKNXT1:	CP 13			;ENTER	RETURN THE SELECTED ITEM
		LD A,B			;SELECTED ITEM
		POP DE
		POP HL
		RET