;=========================
; DS1307
;-----------------------
		
		GLOBAL DS1307_INIT
		GLOBAL DS1307_ADJUST_TEST 					;DEFAULT DATE TIME
		GLOBAL DS1307_ADJUST
		GLOBAL DS1307_GETNOW
		GLOBAL DS1307_SETSQ
		GLOBAL DS1307_GETTIME
		GLOBAL DS1307_SETTIME
		
		DS1307_ADDRESS EQU $68 						;0x68 SHL 1 UPPER 7 BITS
		
		DS1307_CONTROL_REGISTER EQU 0x07
		
		DS1307_RAM_REGISTER EQU 0x08
; DS1307 Control register bits.
		
		RTC_DS1307__RS0 EQU 0x00
		
		RTC_DS1307__RS1 EQU 0x01
		
		RTC_DS1307__SQWE EQU 0x04
		
		RTC_DS1307__OUT EQU 0x07
		
		
		SUN DEFM  "Sunday"
		DB 0
		MON DEFM  "Monday"
		DB 0
		TUE DEFM  "Tuesday"
		DB 0
		WED DEFM  "Wednesday"
		DB 0
		THU DEFM  "Thursday"
		DB 0
		FRI DEFM  "Friday"
		DB 0
		SAT DEFM  "Saturday"
		DB 0
		
		DAYS DEFW SUN
		DEFW MON
		DEFW TUE
		DEFW WED
		DEFW THU
		DEFW FRI
		DEFW SAT
		
		MESERR DEFM "RTC ERR ON RECV"
		DEFB 10,13,0
		
		MESERR0 DEFM "RTC ERR ON SEND"
		DEFB 10,13,0
		
SHERR0:		LD A, E
		PUSH BC
		CALL SOUTAS
		POP BC
		LD A, C
		CALL SOUTAS
		
		LD HL, MESERR0
		CALL RS_TXT
		RET
		
		
SHERR:		LD A, C
		CALL SOUTAS
		LD HL, MESERR
		CALL RS_TXT
		SCF
		RET
		
;RESETS THE BUFFER DATA 20 BYTES
RST_BUF:	XOR A
		LD B, 20
		LD HL, BUFFER
LP1:		LD (HL), A
		INC HL
		DJNZ LP1
		RET
		
DATEMASK:	DEFM "dd/mm/yy w hh:mm:ss"
		
;SET THE MASK FOR DATETIME MESSAGE
DS1307_INIT:	LD HL,DATEMASK
		LD DE,TIMMSG
		LD BC,17
		LDIR								
		RET
		
DS1307_GETNOW:	LD HL, BUFFER
		XOR A 								;SEND COMMAND 0
		LD (HL), A
		LD D, DS1307_ADDRESS
		LD BC, 1
		CALL I2C_WRITE
		JP NZ, SHERR0
;READ 7 BYTES
		LD HL, BUFFER
		LD D, DS1307_ADDRESS
		LD BC, 7
		CALL I2C_READ
		JP NZ, SHERR
		XOR A
		RET
		
;SETS THE TIME ON DS1307 FROM A STRING
;STRING IS ON TIMMSG VAR 'dd/mm/yy w hh:mn:ss' ;w IS DAY OF WEEK 1-7
DS1307_SETTIME:	LD DE, BUFFER
		XOR A
		LD (DE), A
		INC DE
		LD HL,TIMMSG+17		;SS
		CALL SETTMPARAM
		LD HL,TIMMSG+14		;MN
		CALL SETTMPARAM
		LD HL,TIMMSG+11		;HH
		CALL SETTMPARAM
		LD HL,TIMMSG+9		;D OF WEEK 1-7
		CALL SETTMPARAM
		LD HL,TIMMSG		;DD
		CALL SETTMPARAM
		LD HL,TIMMSG+3		;MM
		CALL SETTMPARAM
		LD HL,TIMMSG+6		;YY
		CALL SETTMPARAM
		LD A,0
		LD (DE), A
		CALL DS1307_ADJUST
		RET

SETTMPARAM:	PUSH DE
		CALL strtob
		POP DE
		CALL BN2BCD
		LD A,L
		LD (DE),A
		INC DE		
		RET
		
;SEND COMMAND ADJUST TIME
DS1307_ADJUST_TEST:	
;SET PARAMS
		LD DE, BUFFER
		XOR A
		LD (DE), A
		INC DE
		LD A, 10 							; 10 SECS
		CALL BN2BCD
		LD A, L
		LD (DE), A
		INC DE
		LD A, 34 							; 28 MINS
		CALL BN2BCD
		LD A, L
		LD (DE), A
		INC DE
		LD A, 18 							; 19 HOURS
		CALL BN2BCD
		LD A, L
		LD (DE), A
		INC DE
		LD A, 5 							; DAY OF WEEK
		LD (DE), A
		INC DE
		LD A, 18 							; 8 DAY
		CALL BN2BCD
		LD A, L
		LD (DE), A
		INC DE
		LD A, 02 							; 10 MONTH
		CALL BN2BCD
		LD A, L
		LD (DE), A
		INC DE
		LD A, 21 							; 21 YEAR (2020)
		CALL BN2BCD
		LD A, L
		LD (DE), A
		INC DE
		XOR A
		LD (DE), A
;END OF 9 PARAMS
;9 BYTES OF BUFFER
;ALL BYTES ARE BCD
;0 (CMD)
;SECS
;MINS
;HOURS
;DAY OF WEEK 0-7
;DAY
;MONTH
;YEAR
;YEAR 0
DS1307_ADJUST:	LD HL, BUFFER
		LD D, DS1307_ADDRESS
		LD BC, 9
		CALL I2C_WRITE
		JP NZ, SHERR0
		LD BC, FFFFh 							;8000H FOR 4MHZ
		CALL PAUSE_LOOP 						; 0.5 second delay
		LD BC, FFFFh 							;8000H FOR 4MHZ
		CALL PAUSE_LOOP 						; 0.5 second delay
		RET
		
		
; D HAS THE FREQUENCY PARAM
DS1307_SETSQ:	LD HL, BUFFER
		PUSH HL
		LD A, DS1307_CONTROL_REGISTER
		LD (HL), A
		INC HL
		LD A, D
		LD (HL), A
		POP HL
		LD D, DS1307_ADDRESS
		LD BC, 2
		CALL I2C_WRITE
		JP NZ, SHERR0
		RET
		
		
		
CONVHL:		CALL Bcd2ASC
		LD H, 0
		RET
		
		
CHKERROR:	LD B, 7
		LD HL, BUFFER
CEAGN:		LD A, (HL)
		CP $FF
		JR Z, STER
		INC HL
		DJNZ CEAGN
		XOR A
		RET
STER:										;LD HL,ERRCNT
;INC (HL)
		SCF
		RET
		
;GETS THE TIME AS STRING FROM DS1307
;SET THE DATE AND TIME ON TIMEMSG VAR
;ON ERROR CARRY IS SET
DS1307_GETTIME:	CALL RST_BUF
		CALL DS1307_GETNOW
		RET C 								;ERROR RETURN
		CALL CHKERROR
		RET C 								;ERRREAD  ;IF ERROR DONT PRINT
		LD IX, BUFFER
		LD DE, TIMMSG
		LD H, 0
		LD L, (IX + 4) 							;DAY
		CALL CONVHL
		LD A, ' / '
		LD (DE), A
		INC DE
		LD L, (IX + 5) 							;MONTH
		CALL CONVHL
		LD A, ' / '
		LD (DE), A
		INC DE
		LD L, (IX + 6) 							;YEAR
		CALL CONVHL
		LD A, ' '
		LD (DE), A
		INC DE
		LD L, (IX + 2) 							;HOUR
		CALL CONVHL
		LD A, ':'
		LD (DE), A
		INC DE
		LD L, (IX + 1) 							;MIN
		CALL CONVHL
		LD A, ':'
		LD (DE), A
		INC DE
		LD L, (IX) 							;SECS
		CALL CONVHL
		INC DE
		XOR A
		LD (DE), A
		RET
		
		
		
;=======================
