		MODULE INTERRUPT_SERVICE

@INTSER:	PUSH HL
		PUSH AF
		PUSH BC
		PUSH IX
		
;TEST INTERRUPT MODULE
;AND ACT ON WHAT DEVICE CAUSED THE INTERRUPT
		IN A, (DEVINP)
		CPL
		AND 0X07							;BITS 0-2		
		;DEC A 								;CONVERT 1-8 TO 0-7 NOT NEEDED ON NEW BOARD ONLY ON MY PROTOTYPE
		;CP 8
		;JR NC,IS_EX							;ALWAYS LESS THAN 8
		LD IX, INTTAB
		LD B, 0
		LD C, A
		SLA C								;DOUBLE THIS CAUSE 2 BYTES PER ADDR
		ADD IX, BC
		LD L, (IX)
		LD H, (IX + 1)
		LD BC, IS_EX
		PUSH BC								;RETURN ADDRESS
		JP (HL)								;JP TO HL
		
IS_EX:		POP IX
		POP BC
		POP AF
		POP HL
		EI
		RET								;SAME AS RETI FOR IM1
		
;IT COPIES THE INTERRUPT TABLE TO RAM SYS VARS
;ALSO ENABLES INTERRUPTS
@COPYINTTAB:	DI
		LD HL,MYINTADDR
		LD DE,INTTAB
		LD BC,16
		LDIR
		EI 
		RET	

MYINTADDR:	DEFW INT_TIMER
		DEFW SAVETE
		DEFW INT_KEYB
		DEFW DOCLRINT
		DEFW DOCLRINT
		DEFW DOCLRINT
		DEFW DOCLRINT
		DEFW DOCLRINT
		
INT_KEYB:	CALL PS2SETKEYAV						;SET NEW KEY AVAILABLE FROM PS/2
		OUT (DEVINT), A 						;CLEAR THE INTERRUPT		
		RET


SAVETE:		OUT (DEVINT), A 						;CLEAR THE INTERRUPT
		LD A, 1
		LD (INT2TE),A 							;TEARING EFFECT
		RET
		
		
DOCLRINT:	OUT (DEVINT), A
		RET
		
INT_TIMER:	OUT (DEVINT), A
		LD HL, TIMECTR
		INC (HL)
		LD A, (HL)
		CP 0
		RET NZ
		INC HL
		INC (HL)
		LD A, (HL)
		CP 0
		RET NZ
		INC HL
		INC (HL)
		LD A, (HL)
		CP 0
		RET NZ
		INC HL
		INC (HL)
		LD A, (HL)
		CP 0
		RET NZ
;ZERO ALL 4 BYTES
		LD HL, 0
		LD (TIMECTR), HL
		LD (TIMECTR + 2), HL
		RET
