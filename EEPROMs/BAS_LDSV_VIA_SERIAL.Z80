;Load / Save using RS232 Serial Port

;LOCAL CONSTS
PORT_TAPE       EQU DEVTAP      ; SAVE/LOAD PORT 1 BYTE=0xDD 2ND BYTE=COMMAND 
COMMD_SAVE      EQU 10
COMMD_LOAD      EQU 20;

@MYOS_OSSAVE:
            ;bc = length of program
            ;hl = ACCS =FILENAME ENDS WITH 0X0D
            ;TOP = CONTAINS START OF BASIC PROGRAM LD HL,(TOP)
            ;OUT SAVE COMMAND (TWO BYTES)            
            ;OUT THE FILENAME 0X0D TERMINATED
            ;OUT THE LENGTH (TWO BYTES)            
            ;OUT THE BYTES
            ;COMMAND
            LD A,0xDD
            CALL RS_TX
            LD A,COMMD_SAVE
            CALL RS_TX
            ;FILENAME
OSSV_NXT:   LD A,(HL)
            CALL RS_TX
            INC HL
            CP 0x0D
            JR NZ,OSSV_NXT
            ;LENGTH
            LD A,C
            CALL RS_TX
            LD A,B
            CALL RS_TX
            ;BYTES
            LD HL,(PAGE)
OSSV_NXT2:  LD A,(HL)
            CALL RS_TX
            INC HL
            DEC BC
            LD A,B
            OR C
            JR NZ,OSSV_NXT2                        
            RET              
@MYOS_OSLOAD:
            ;OUT LOAD COMMAND (TWO BYTES)
            ;OUT THE FILENAME 0X0D TERMINATED
            ;IN THE LENGTH (TWO BYTES)
            ;IN THE BYTES 
            LD A,0xDD
            CALL RS_TX
            LD A,COMMD_LOAD
            CALL RS_TX
            ;FILENAME
OSLD_NXT:   LD A,(HL)
            CALL RS_TX
            INC HL
            CP 0x0D
            JR NZ,OSLD_NXT
            ;LENGTH READ
            CALL RS_RX
            LD C,A
            CALL RS_RX
            LD B,A
            OR A
            JR NZ,OK1
            LD A,C
            OR A
            JR NZ,OK1
            ;ERROR FILE SIZE IS 0
            CALL RS_RX ;GET THE ERRORCODE            
            JP LDERR
            
OK1:        ;BYTES
            LD HL,(PAGE)                        
OSLD_NXT2:  CALL RS_RX
            LD (HL),A
            INC HL
            DEC BC
            LD A,B
            OR C
            JR NZ,OSLD_NXT2                        
            LD B,H
            LD C,L
            LD HL,TOP ;SAVE TOP OF BASIC 
            LD (HL),C
            INC HL
            LD (HL),B            
            SCF       ;SET C = NO ERROR            
            RET   
            
LDERR:    	CP 4
            JR Z,FLNOTF
            CALL    EXTERR
        		DEFM    'Error Loading'
        		DEFB    0			
FLNOTF:     CALL    EXTERR
        		DEFM    'Wrong Filename'
        		DEFB    0			
      